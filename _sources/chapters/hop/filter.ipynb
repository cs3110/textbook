{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "d41ef3dd",
   "metadata": {},
   "source": [
    "# Filter\n",
    "\n",
    "{{ video_embed | replace(\"%%VID%%\", \"FaWtD-LRdpU\")}}\n",
    "\n",
    "Suppose we wanted to filter out only the even numbers from a list, or the odd\n",
    "numbers.  Here are some functions to do that:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "cf4f2563",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "val even : int -> bool = <fun>\n"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    },
    {
     "data": {
      "text/plain": [
       "val evens : int list -> int list = <fun>\n"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    },
    {
     "data": {
      "text/plain": [
       "val lst1 : int list = [2; 4]\n"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "(** [even n] is whether [n] is even. *)\n",
    "let even n =\n",
    "  n mod 2 = 0\n",
    "\n",
    "(** [evens lst] is the sublist of [lst] containing only even numbers. *)\n",
    "let rec evens = function\n",
    "  | [] -> []\n",
    "  | h :: t -> if even h then h :: evens t else evens t\n",
    "\n",
    "let lst1 = evens [1; 2; 3; 4]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "56c85f66",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "val odd : int -> bool = <fun>\n"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    },
    {
     "data": {
      "text/plain": [
       "val odds : int list -> int list = <fun>\n"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    },
    {
     "data": {
      "text/plain": [
       "val lst2 : int list = [1; 3]\n"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "(** [odd n] is whether [n] is odd. *)\n",
    "let odd n =\n",
    "  n mod 2 <> 0\n",
    "\n",
    "(** [odds lst] is the sublist of [lst] containing only odd numbers. *)\n",
    "let rec odds = function\n",
    "  | [] -> []\n",
    "  | h :: t -> if odd h then h :: odds t else odds t\n",
    "\n",
    "let lst2 = odds [1; 2; 3; 4]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b7d7254d",
   "metadata": {},
   "source": [
    "Functions `evens` and `odds` are nearly the same code: the only essential\n",
    "difference is the test they apply to the head element. So as we did with `map`\n",
    "in the previous section, let's factor out that test as a function. Let's name\n",
    "the function `p` as short for \"predicate\", which is a fancy way of saying\n",
    "that it tests whether something is true or false:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "3fb3a597",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "val filter : ('a -> bool) -> 'a list -> 'a list = <fun>\n"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "let rec filter p = function\n",
    "  | [] -> []\n",
    "  | h :: t -> if p h then h :: filter p t else filter p t"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0553a169",
   "metadata": {},
   "source": [
    "And now we can reimplement our original two functions:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "cc0271da",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "val evens : int list -> int list = <fun>\n"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    },
    {
     "data": {
      "text/plain": [
       "val odds : int list -> int list = <fun>\n"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "let evens = filter even\n",
    "let odds = filter odd"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b336d7c8",
   "metadata": {},
   "source": [
    "How simple these are! How clear! (At least to the reader who is familiar with\n",
    "`filter`.)\n",
    "\n",
    "## Filter and Tail Recursion\n",
    "\n",
    "As we did with `map`, we can create a tail-recursive version of `filter`:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "7b49855f",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "val filter_aux : ('a -> bool) -> 'a list -> 'a list -> 'a list = <fun>\n"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    },
    {
     "data": {
      "text/plain": [
       "val filter : ('a -> bool) -> 'a list -> 'a list = <fun>\n"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    },
    {
     "data": {
      "text/plain": [
       "val lst : int list = [4; 2]\n"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "let rec filter_aux p acc = function\n",
    "  | [] -> acc\n",
    "  | h :: t -> if p h then filter_aux p (h :: acc) t else filter_aux p acc t\n",
    "\n",
    "let filter p = filter_aux p []\n",
    "\n",
    "let lst = filter even [1; 2; 3; 4]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "aef428f8",
   "metadata": {},
   "source": [
    "And again we discover the output is backwards. Here, the standard library makes\n",
    "a different choice than it did with `map`. It builds in the reversal to\n",
    "`List.filter`, which is implemented like this:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "5b7d49c8",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "val filter_aux : ('a -> bool) -> 'a list -> 'a list -> 'a list = <fun>\n"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    },
    {
     "data": {
      "text/plain": [
       "val filter : ('a -> bool) -> 'a list -> 'a list = <fun>\n"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "let rec filter_aux p acc = function\n",
    "  | [] -> List.rev acc (* note the built-in reversal *)\n",
    "  | h :: t -> if p h then filter_aux p (h :: acc) t else filter_aux p acc t\n",
    "\n",
    "let filter p = filter_aux p []"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2fbe9a76",
   "metadata": {},
   "source": [
    "Why does the standard library treat `map` and `filter` differently on this\n",
    "point? Good question. Perhaps there has simply never been a demand for a\n",
    "`filter` function whose time efficiency is a constant factor better. Or perhaps\n",
    "it is just historical accident.\n",
    "\n",
    "## Filter in Other Languages\n",
    "\n",
    "Again, the idea of filter exists in many programming languages. Here it is in\n",
    "Python:\n",
    "```python\n",
    ">>> print(list(filter(lambda x: x % 2 == 0, [1, 2, 3, 4])))\n",
    "[2, 4]\n",
    "```\n",
    "And in Java:\n",
    "```java\n",
    "jshell> Stream.of(1, 2, 3, 4).filter(x -> x % 2 == 0).collect(Collectors.toList())\n",
    "$1 ==> [2, 4]\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "cell_metadata_filter": "-all",
   "formats": "md:myst",
   "text_representation": {
    "extension": ".md",
    "format_name": "myst",
    "format_version": 0.13,
    "jupytext_version": "1.10.3"
   }
  },
  "kernelspec": {
   "display_name": "OCaml",
   "language": "OCaml",
   "name": "ocaml-jupyter"
  },
  "language_info": {
   "codemirror_mode": "text/x-ocaml",
   "file_extension": ".ml",
   "mimetype": "text/x-ocaml",
   "name": "OCaml",
   "nbconverter_exporter": null,
   "pygments_lexer": "OCaml",
   "version": "4.11.1"
  },
  "source_map": [
   14,
   23,
   36,
   47,
   55,
   59,
   63,
   66,
   75,
   83,
   89,
   95
  ]
 },
 "nbformat": 4,
 "nbformat_minor": 5
}