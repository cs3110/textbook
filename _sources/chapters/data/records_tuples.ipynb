{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "444b3b65",
   "metadata": {},
   "source": [
    "# Records and Tuples\n",
    "\n",
    "{{ video_embed | replace(\"%%VID%%\", \"E3d1IFMU558\")}}\n",
    "\n",
    "Singly-linked lists are a great data structure, but what if you want a fixed\n",
    "number of elements, instead of an unbounded number? Or what if you want the\n",
    "elements to have distinct types? Or what if you want to access the elements by\n",
    "name instead of by number? Lists don't make any of those possibilities easy.\n",
    "Instead, OCaml programmers use records and tuples.\n",
    "\n",
    "## Records\n",
    "\n",
    "A *record* is a composite of other types of data, each of which is named. OCaml\n",
    "records are much like structs in C. Here's an example of a record type\n",
    "definition `mon` for a Pok&eacute;<u>mon</u>, re-using the `ptype` definition\n",
    "from the [variants](variants) section:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8dd42d5c",
   "metadata": {},
   "outputs": [],
   "source": [
    "type ptype = TNormal | TFire | TWater\n",
    "type mon = {name : string; hp : int; ptype : ptype}"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "392d5dee",
   "metadata": {},
   "source": [
    "This type defines a record with three *fields* named `name`, `hp` (hit points),\n",
    "and `ptype`. The type of each of those fields is also given. Note that `ptype`\n",
    "can be used as both a type name and a field name; the *namespace* for those is\n",
    "distinct in OCaml.\n",
    "\n",
    "To build a value of a record type, we write a record expression, which looks\n",
    "like this:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cc9bc40a",
   "metadata": {},
   "outputs": [],
   "source": [
    "{name = \"Charmander\"; hp = 39; ptype = TFire}"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4ea860c7",
   "metadata": {},
   "source": [
    "So in a type definition we write a colon between the name and the type of a\n",
    "field, but in an expression we write an equals sign.\n",
    "\n",
    "To access a record and get a field from it, we use the dot notation that you\n",
    "would expect from many other languages. For example:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "575ae5f6",
   "metadata": {},
   "outputs": [],
   "source": [
    "let c = {name = \"Charmander\"; hp = 39; ptype = TFire};;\n",
    "c.hp"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "512a3a78",
   "metadata": {},
   "source": [
    "It's also possible to use pattern matching to access record fields:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6bbb89a9",
   "metadata": {},
   "outputs": [],
   "source": [
    "match c with {name = n; hp = h; ptype = t} -> h"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1cdec0f5",
   "metadata": {},
   "source": [
    "The `n`, `h`, and `t` here are pattern variables. There is a syntactic sugar\n",
    "provided if you want to use the same name for both the field and a pattern\n",
    "variable:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "109ac766",
   "metadata": {},
   "outputs": [],
   "source": [
    "match c with {name; hp; ptype} -> hp"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ccda1754",
   "metadata": {},
   "source": [
    "Here, the pattern `{name; hp; ptype}` is sugar for\n",
    "`{name = name; hp = hp; ptype = ptype}`. In each of those subexpressions, the\n",
    "identifier appearing on the left-hand side of the equals is a field name, and\n",
    "the identifier appearing on the right-hand side is a pattern variable.\n",
    "\n",
    "{{ video_embed | replace(\"%%VID%%\", \"gXlWHvEoIvg\")}}\n",
    "\n",
    "**Syntax.**\n",
    "\n",
    "A record expression is written:\n",
    "```ocaml\n",
    "{f1 = e1; ...; fn = en}\n",
    "```\n",
    "The order of the `fi=ei` inside a record expression is irrelevant.\n",
    "For example, `{f = e1; g = e2}` is entirely equivalent to `{g = e2; f = e1}`.\n",
    "\n",
    "A field access is written:\n",
    "```ocaml\n",
    "e.f\n",
    "```\n",
    "where `f` must be an identifier of a field name, not an expression. That\n",
    "restriction is the same as in any other language with similar\n",
    "features---&mdash;for example, Java field names. If you really do want to\n",
    "*compute* which identifier to access, then actually you want a different data\n",
    "structure: a *map* (also known by many other names: a *dictionary* or\n",
    "*association list* or *hash table* etc., though there are subtle differences\n",
    "implied by each of those terms.)\n",
    "\n",
    "**Dynamic semantics.**\n",
    "\n",
    "* If for all `i` in `1..n`, it holds that `ei ==> vi`, then\n",
    "  `{f1 = e1; ...; fn = en} ==> {f1 = v1; ...; fn = vn}`.\n",
    "\n",
    "* If `e ==> {...; f = v; ...}` then `e.f ==> v`.\n",
    "\n",
    "**Static semantics.**\n",
    "\n",
    "A record type is written:\n",
    "```ocaml\n",
    "{f1 : t1; ...; fn : tn}\n",
    "```\n",
    "The order of the `fi:ti` inside a record type is irrelevant. For example,\n",
    "`{f : t1; g : t2}` is entirely equivalent to `{g:t2;f:t1}`.\n",
    "\n",
    "Note that record types must be defined before they can be used.  This\n",
    "enables OCaml to do better type inference than would be possible if\n",
    "record types could be used without definition.\n",
    "\n",
    "The type checking rules are:\n",
    "\n",
    "* If for all `i` in `1..n`, it holds that `ei : ti`, and if `t` is defined to be\n",
    "  `{f1 : t1; ...; fn : tn}`, then `{f1 = e1; ...; fn = en} : t`. Note that the\n",
    "  set of fields provided in a record expression must be the full set of fields\n",
    "  defined as part of the record's type (but see below regarding *record copy*).\n",
    "\n",
    "* If `e : t1` and if `t1` is defined to be `{...; f : t2; ...}`, then\n",
    "  `e.f : t2`.\n",
    "\n",
    "**Record copy.**\n",
    "\n",
    "Another syntax is also provided to construct a new record out of an old record:\n",
    "```ocaml\n",
    "{e with f1 = e1; ...; fn = en}\n",
    "```\n",
    "This doesn't mutate the old record. Rather, it constructs a new record with new\n",
    "values. The set of fields provided after the `with` does not have to be the full\n",
    "set of fields defined as part of the record's type. In the newly-copied record,\n",
    "any field not provided as part of the `with` is copied from the old record.\n",
    "\n",
    "Record copy is syntactic sugar. It's equivalent to writing\n",
    "```ocaml\n",
    "{ f1 = e1;   ...; fn = en;\n",
    "  g1 = e.g1; ...; gn = e.gn }\n",
    "```\n",
    "where the set of `gi` is the set of all fields of the record's type minus the\n",
    "set of `fi`.\n",
    "\n",
    "**Pattern matching.**\n",
    "\n",
    "We add the following new pattern form to the list of legal patterns:\n",
    "\n",
    "* `{f1 = p1; ...; fn = pn}`\n",
    "\n",
    "And we extend the definition of when a pattern matches a value and produces a\n",
    "binding as follows:\n",
    "\n",
    "* If for all `i` in `1..n`, it holds that `pi` matches `vi` and produces\n",
    "  bindings $b_i$, then the record pattern `{f1 = p1; ...; fn = pn}` matches the\n",
    "  record value `{f1 = v1; ...; fn = vn; ...}` and produces the set $\\bigcup_i\n",
    "  b_i$ of bindings. Note that the record value may have more fields than the\n",
    "  record pattern does.\n",
    "\n",
    "As a syntactic sugar, another form of record pattern is provided:\n",
    "`{f1; ...; fn}`. It is desugared to `{f1 = f1; ...; fn = fn}`.\n",
    "\n",
    "## Tuples\n",
    "\n",
    "Like records, *tuples* are a composite of other types of data. But instead of\n",
    "naming the *components*, they are identified by position. Here are some examples\n",
    "of tuples:\n",
    "```ocaml\n",
    "(1, 2, 10)\n",
    "(true, \"Hello\")\n",
    "([1; 2; 3], (0.5, 'X'))\n",
    "```\n",
    "A tuple with two components is called a *pair*. A tuple with three components is\n",
    "called a *triple*. Beyond that, we usually just use the word *tuple* instead of\n",
    "continuing a naming scheme based on numbers.\n",
    "\n",
    "```{tip}\n",
    "Beyond about three components, it's arguably better to use records instead of\n",
    "tuples, because it becomes hard for a programmer to remember which component was\n",
    "supposed to represent what information.\n",
    "```\n",
    "\n",
    "Building of tuples is easy: just write the tuple, as above. Accessing again\n",
    "involves pattern matching, for example:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "78478393",
   "metadata": {},
   "outputs": [],
   "source": [
    "match (1, 2, 3) with (x, y, z) -> x + y + z"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "60a58207",
   "metadata": {},
   "source": [
    "{{ video_embed | replace(\"%%VID%%\", \"4QNzC2KZ5I4\")}}\n",
    "\n",
    "**Syntax.**\n",
    "\n",
    "A tuple is written\n",
    "```ocaml\n",
    "(e1, e2, ..., en)\n",
    "```\n",
    "The parentheses are not entirely mandatory &mdash;often your code can\n",
    "successfully parse without them&mdash; but they are usually considered to be\n",
    "good style to include.\n",
    "\n",
    "**Dynamic semantics.**\n",
    "\n",
    "* If for all `i` in `1..n` it holds that `ei ==> vi`, then\n",
    "  `(e1, ..., en) ==> (v1, ..., vn)`.\n",
    "\n",
    "**Static semantics.**\n",
    "\n",
    "Tuple types are written using a new type constructor `*`, which is different\n",
    "than the multiplication operator. The type `t1 * ... * tn` is the type of tuples\n",
    "whose first component has type `t1`, ..., and nth component has type `tn`.\n",
    "\n",
    "* If for all `i` in `1..n` it holds that `ei : ti`, then\n",
    "  `(e1, ..., en) : t1 * ... * tn`.\n",
    "\n",
    "**Pattern matching.**\n",
    "\n",
    "We add the following new pattern form to the list of legal patterns:\n",
    "\n",
    "* `(p1, ..., pn)`\n",
    "\n",
    "The parentheses are again not entirely mandatory but usually are idiomatic to\n",
    "include.\n",
    "\n",
    "And we extend the definition of when a pattern matches a value and produces a\n",
    "binding as follows:\n",
    "\n",
    "* If for all `i` in `1..n`, it holds that `pi` matches `vi` and produces\n",
    "  bindings $b_i$, then the tuple pattern `(p1, ..., pn)` matches the tuple value\n",
    "  `(v1, ..., vn)` and produces the set $\\bigcup_i b_i$ of bindings. Note that\n",
    "  the tuple value must have exactly the same number of components as the tuple\n",
    "  pattern does.\n",
    "\n",
    "## Variants vs. Tuples and Records\n",
    "\n",
    "{{ video_embed | replace(\"%%VID%%\", \"9kyOH1kpmjk\")}}\n",
    "\n",
    "{{ video_embed | replace(\"%%VID%%\", \"oMOO-cWrHuw\")}}\n",
    "\n",
    "```{note}\n",
    "The second video above uses more advanced examples of variants that will be\n",
    "studied in a [later section](algebraic_data_types).\n",
    "```\n",
    "\n",
    "The big difference between variants and the types we just learned (records and\n",
    "tuples) is that a value of a variant type is *one of* a set of possibilities,\n",
    "whereas a value of a tuple or record type provides *each of* a set of\n",
    "possibilities. Going back to our examples, a value of type `day` is **one of**\n",
    "`Sun` or `Mon` or etc. But a value of type `mon` provides **each of** a `string`\n",
    "and an `int` and `ptype`. Note how, in those previous two sentences, the word\n",
    "\"or\" is associated with variant types, and the word \"and\" is associated with\n",
    "tuple and record types. That's a good clue if you're ever trying to decide\n",
    "whether you want to use a variant, or a tuple or record: if you need one piece\n",
    "of data *or* another, you want a variant; if you need one piece of data *and*\n",
    "another, you want a tuple or record.\n",
    "\n",
    "One-of types are more commonly known as *sum types*, and each-of types as\n",
    "*product types*. Those names come from set theory. Variants are like\n",
    "[disjoint union][disjun], because each value of a variant comes from one of many\n",
    "underlying sets (and thus far each of those sets is just a single constructor\n",
    "hence has cardinality one). Disjoint union is indeed sometimes written with a\n",
    "summation operator $\\Sigma$. Tuples/records are like\n",
    "[Cartesian product][cartprod], because each value of a tuple or record contains\n",
    "a value from each of many underlying sets. Cartesian product is usually written\n",
    "with a product operator, $\\times$ or $\\Pi$.\n",
    "\n",
    "[disjun]: https://en.wikipedia.org/wiki/Disjoint_union\n",
    "[cartprod]: https://en.wikipedia.org/wiki/Cartesian_product"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "cell_metadata_filter": "-all",
   "formats": "md:myst",
   "text_representation": {
    "extension": ".md",
    "format_name": "myst",
    "format_version": 0.13,
    "jupytext_version": "1.10.3"
   }
  },
  "kernelspec": {
   "display_name": "OCaml",
   "language": "OCaml",
   "name": "ocaml-jupyter"
  },
  "source_map": [
   14,
   32,
   35,
   43,
   45,
   51,
   54,
   57,
   59,
   63,
   65,
   183,
   185
  ]
 },
 "nbformat": 4,
 "nbformat_minor": 5
}