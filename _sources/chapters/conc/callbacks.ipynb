{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "540907a3",
   "metadata": {},
   "source": [
    "# Callbacks\n",
    "\n",
    "For a program to benefit from the concurrency provided by asynchronous\n",
    "I/O and promises, there needs to be a way for the program to make use of\n",
    "resolved promises.  For example, if a web server is asynchronously\n",
    "reading and serving multiple files to multiple clients, the server needs\n",
    "a way to (i) become aware that a read has completed, and (ii) then do a\n",
    "new asynchronous write with the result of the read. In other words,\n",
    "programs need a mechanism for managing the dependencies among promises.\n",
    "\n",
    "The mechanism provided in Lwt is named *callbacks.*\n",
    "A callback is a function that, when \"registered\" with a promise, will be\n",
    "run sometime after that promise has been fulfilled.\n",
    "The callback will receive as input the contents of the fulfilled promise.\n",
    "Think of it like asking your friend to do some math for you: they\n",
    "promise to do it, and to call you back on the phone with the answer sometime after they've finished.\n",
    "You can do other things while you wait for them to call you back, and when they do, you can use the answer they give you.\n",
    "\n",
    "## Registering a Callback\n",
    "\n",
    "Here is a function that prints a string using Lwt's\n",
    "version of the `printf` function:\n",
    "```ocaml\n",
    "let print_the_string str = Lwt_io.printf \"The string is: %S\\n\" str\n",
    "```\n",
    "\n",
    "And here, repeated from the previous section, is our code that returns a promise\n",
    "for a string read from standard input:\n",
    "```ocaml\n",
    "let p = read_line stdin\n",
    "```\n",
    "\n",
    "To register the printing function as a callback for the promise `p`, we use the\n",
    "function `Lwt.bind`, which *binds* the callback to the promise:\n",
    "\n",
    "```ocaml\n",
    "Lwt.bind p print_the_string\n",
    "```\n",
    "\n",
    "Sometime after `p` is fulfilled, hence contains a string, the callback\n",
    "will be run with that string as its input. That will cause the string to be printed.\n",
    "\n",
    "Here's a complete utop transcript as an example of that:\n",
    "```text\n",
    "# let print_the_string str = Lwt_io.printf \"The string is: %S\\n\" str;;\n",
    "val print_the_string : string -> unit Lwt.t = <fun>\n",
    "# let p = read_line stdin in Lwt.bind p print_the_string;;\n",
    "- : unit Lwt.t = <abstr>\n",
    "  <type Camels are bae followed by Enter>\n",
    "# The string is: \"Camels are bae\"\n",
    "```\n",
    "\n",
    "## Bind\n",
    "\n",
    "The type of `Lwt.bind` is important to understand:\n",
    "```ocaml\n",
    "'a Lwt.t -> ('a -> 'b Lwt.t) -> 'b Lwt.t\n",
    "```\n",
    "\n",
    "The `bind` function takes a promise as its first argument. It doesn't matter\n",
    "whether that promise has been resolved yet. As its second argument,\n",
    "`bind` takes a callback. Recall that a callback is a function: indeed, this callback takes an input which is the same\n",
    "type `'a` as the contents of the promise. It's not an accident that they have\n",
    "the same type: the whole idea is to eventually run the callback on the fulfilled\n",
    "promise, so the type that the promise contains needs to be the same as the type that the\n",
    "callback expects as input.\n",
    "\n",
    "After being invoked on a promise and callback, e.g., `bind p c`, the `bind`\n",
    "function does one of three things, depending on the state of `p`:\n",
    "\n",
    "* If `p` is already fulfilled, then `c` is run immediately on the contents of\n",
    "  `p`. The promise that is returned might or might not be pending, depending on\n",
    "  what `c` does.\n",
    "\n",
    "* If `p` is already rejected, then `c` does not run. The promise that is\n",
    "  returned is also rejected, with the same exception as `p`.\n",
    "\n",
    "* If `p` is pending, then `bind` does not wait for `p` to be resolved, nor for\n",
    "  `c` to be run. Rather, `bind` just registers the callback to eventually be run\n",
    "  when (or if) the promise is fulfilled. Therefore, the `bind` function returns a\n",
    "  new promise. That promise may become resolved when (or if) the callback\n",
    "  completes running, sometime in the future. Its contents will be whatever\n",
    "  contents are contained within the promise that the callback itself returns.\n",
    "\n",
    "```{note}\n",
    "For the first case above: The Lwt source code claims that this behavior might\n",
    "change: under high load, `c` might be registered to run\n",
    "later. But as of [v5.5.0][lwt-bind-src] that behavior has not yet been activated. So, don't\n",
    "worry about it&mdash;this paragraph is just here to future-proof this\n",
    "discussion.\n",
    "```\n",
    "\n",
    "[lwt-bind-src]: https://github.com/ocsigen/lwt/blob/73f1a0f0acd5540f25e58bc410e1f63271189c6c/src/core/lwt.ml#L1820\n",
    "\n",
    "Let's consider that final case in more detail. We have one promise of type\n",
    "`'a Lwt.t` and two promises of type `'b Lwt.t`:\n",
    "\n",
    "* The promise of type `'a Lwt.t`, call it promise X, is an input to `bind`. It\n",
    "  was pending when `bind` was called, and when `bind` returns.\n",
    "\n",
    "* The first promise of type `'b Lwt.t`, call it promise Y, is created by `bind`\n",
    "  and immediately returned to the user. It is pending at that point.\n",
    "\n",
    "* The second promise of type `'b Lwt.t`, call it promise Z, has not yet been\n",
    "  created. It may be created later: if promise X is fulfilled, the\n",
    "  callback will run on the contents of X, and the callback will return promise\n",
    "  Z. There is no guarantee about the state of Z; it might well still be pending\n",
    "  when returned by the callback.\n",
    "\n",
    "* If Z is finally fulfilled, the contents of Y are updated to be the same as\n",
    "  the contents of Z. If Z is rejected, then Y is also rejected with the same\n",
    "  exception. If Z remains pending, then Y remains pending as well.\n",
    "  Recall that Y has already been returned to the user.\n",
    "  \"Relaying\" the contents of Z to Y ensures that the user can eventually benefit\n",
    "  from the chain of operations that started with promise X.\n",
    "\n",
    "The reason why `bind` is designed with this type is so that programmers can set\n",
    "up a *sequential chain* of callbacks. For example, the following code\n",
    "asynchronously reads one string; then when that string has been read, proceeds\n",
    "to asynchronously read a second string; then prints the concatenation of both\n",
    "strings:\n",
    "\n",
    "```ocaml\n",
    "Lwt.bind (read_line stdin) (fun s1 ->\n",
    "  Lwt.bind (read_line stdin) (fun s2 ->\n",
    "    Lwt_io.printf \"%s\\n\" (s1^s2)));;\n",
    "```\n",
    "\n",
    "If you run that in utop, something slightly confusing will happen again: after\n",
    "you press Enter at the end of the first string, Lwt will allow utop to read one\n",
    "character. The problem is that we're mixing Lwt input operations with utop input\n",
    "operations. It would be better to just create a program and run it from the\n",
    "command line.\n",
    "\n",
    "To do that, put the following code in a file called `read2.ml`:\n",
    "```ocaml\n",
    "open Lwt_io\n",
    "\n",
    "let p =\n",
    "  Lwt.bind (read_line stdin) (fun s1 ->\n",
    "    Lwt.bind (read_line stdin) (fun s2 ->\n",
    "      Lwt_io.printf \"%s\\n\" (s1^s2)))\n",
    "\n",
    "let _ = Lwt_main.run p\n",
    "```\n",
    "\n",
    "We've added one new function: `Lwt_main.run : 'a Lwt.t -> 'a`. It waits for its\n",
    "input promise to be fulfilled, then returns the contents. This function\n",
    "is called only once in an entire program, near the end of the main file; and the\n",
    "input to it is typically a promise whose resolution indicates that all execution\n",
    "is finished.\n",
    "\n",
    "Create a dune file:\n",
    "```text\n",
    "(executable\n",
    " (name read2)\n",
    " (libraries lwt.unix))\n",
    "```\n",
    "\n",
    "And run the program, entering a couple strings:\n",
    "```console\n",
    "dune exec ./read2.exe\n",
    "My first string\n",
    "My second string\n",
    "My first stringMy second string\n",
    "```\n",
    "\n",
    "Now try removing the last line of `read2.ml`.  You'll see that the program\n",
    "exits immediately, without waiting for you to type.\n",
    "\n",
    "## Bind as an Operator\n",
    "\n",
    "The `Lwt.Infix` module defines an\n",
    "infix operator written `>>=` that is the same as `bind`. That is, instead of\n",
    "writing `bind p c` you write `p >>= c`. This operator makes it much easier to\n",
    "write code without all the extra parentheses and indentations that our previous\n",
    "example had:\n",
    "\n",
    "```ocaml\n",
    "open Lwt_io\n",
    "open Lwt.Infix\n",
    "\n",
    "let p =\n",
    "  read_line stdin >>= fun s1 ->\n",
    "  read_line stdin >>= fun s2 ->\n",
    "  Lwt_io.printf \"%s\\n\" (s1^s2)\n",
    "\n",
    "let _ = Lwt_main.run p\n",
    "```\n",
    "\n",
    "The way to visually parse the definition of `p` is to look at each line as\n",
    "computing some promised value. The first line, `read_line stdin >>= fun s1 ->`\n",
    "means that a promise is created, fulfilled, and its contents extracted under the\n",
    "name `s1`. The second line means the same, except that its contents are named\n",
    "`s2`. The third line creates a final promise whose contents are eventually\n",
    "extracted by `Lwt_main.run`, at which point the program may terminate.\n",
    "\n",
    "The `>>=` operator is perhaps most famous from the functional language Haskell,\n",
    "which uses it extensively for monads. We'll cover monads in a later section.\n",
    "\n",
    "## Bind as Let Syntax\n",
    "\n",
    "There is a *syntax extension* for OCaml that makes using\n",
    "bind even simpler than the infix operator `>>=`. To install the syntax\n",
    "extension, run the following command:\n",
    "\n",
    "`$ opam install lwt_ppx`\n",
    "\n",
    "With that extension, you can use a specialized `let` expression written\n",
    "`let%lwt x = e1 in e2`, which is equivalent to `bind e1 (fun x -> e2)` or\n",
    "`e1 >>= fun x -> e2`. We can rewrite our running example as follows:\n",
    "\n",
    "```ocaml\n",
    "open Lwt_io\n",
    "\n",
    "let p =\n",
    "  let%lwt s1 = read_line stdin in\n",
    "  let%lwt s2 = read_line stdin in\n",
    "  Lwt_io.printf \"%s\\n\" (s1^s2)\n",
    "\n",
    "let _ = Lwt_main.run p\n",
    "```\n",
    "\n",
    "````{note}\n",
    "To compile that code as part of a Dune project, use a `dune` file like this:\n",
    "```\n",
    "(executable\n",
    " (public_name ...)\n",
    " (name ...)\n",
    " (libraries lwt.unix ...)\n",
    " (preprocess (pps lwt_ppx)))\n",
    "```\n",
    "The `libraries` stanza loads `lwt.unix` so that we can use `Lwt_io`, and the `preprocess` stanza makes `let%lwt` available through `lwt_ppx`.\n",
    "````\n",
    "\n",
    "Now the code looks pretty much exactly like what its equivalent synchronous\n",
    "version would be. But don't be fooled: all the asynchronous I/O, the promises,\n",
    "and the callbacks are still there. Thus, the evaluation of `p` first registers a\n",
    "callback with a promise, then moves on to the evaluation of `Lwt_main.run`\n",
    "without waiting for the first string to finish being read. To prove that to\n",
    "yourself, run the following code:\n",
    "\n",
    "```ocaml\n",
    "open Lwt_io\n",
    "\n",
    "let p =\n",
    "  let%lwt s1 = read_line stdin in\n",
    "  let%lwt s2 = read_line stdin in\n",
    "  Lwt_io.printf \"%s\\n\" (s1 ^ s2)\n",
    "\n",
    "let _ = Lwt_io.printf \"Got here first\\n\"\n",
    "\n",
    "let _ = Lwt_main.run p\n",
    "```\n",
    "\n",
    "You'll see that \"Got here first\" prints before you get a chance to enter any\n",
    "input.\n",
    "\n",
    "**Another `let` syntax.**\n",
    "Instead of loading the additional library `lwt_ppx` to make `let%lwt` available, we can use a similar `let*` syntax provided by `Lwt.Syntax`:\n",
    "\n",
    "```ocaml\n",
    "open Lwt_io\n",
    "open Lwt.Syntax\n",
    "\n",
    "let p =\n",
    "  let* s1 = read_line stdin in\n",
    "  let* s2 = read_line stdin in\n",
    "  Lwt_io.printf \"%s\\n\" (s1 ^ s2)\n",
    "```\n",
    "\n",
    "But we generally prefer to use `lwt_ppx`, because it also makes some other useful syntax available.\n",
    "For example, `lwt_ppx` also makes `try%lwt` available, and that is useful for exception handling that involves promises.\n",
    "\n",
    "## Concurrent Composition\n",
    "\n",
    "The `Lwt.bind` function provides a way to\n",
    "sequentially compose callbacks: first one callback is run, then another, then\n",
    "another, and so forth. There are other functions in the library for composition\n",
    "of many callbacks as a set. For example,\n",
    "\n",
    "* `Lwt.map : 'a Lwt.t -> ('a -> 'b) -> 'b Lwt.t` is a lot like `Lwt.bind`,\n",
    "  but its callback immediately returns a *value* of type `'b`, not the\n",
    "  *promise* of a value of type `'b`. `Lwt.map p f` returns a promise that is\n",
    "  pending until `p` is resolved. If `p` is resolved via rejection, the callback is\n",
    "  never called and the pending promise is rejected with the same exception. If\n",
    "  `p` is resolved via fulfilment (say with value `v`), then the pending promise\n",
    "  is resolved with `f v`. Note that `f` may itself raise an exception, in which\n",
    "  case the pending promise is rejected with that exception.\n",
    "\n",
    "* `Lwt.join : unit Lwt.t list -> unit Lwt.t` enables waiting upon multiple\n",
    "  promises. `Lwt.join ps` returns a promise that is pending until all the\n",
    "  promises in `ps` become resolved. You might register a callback on the return\n",
    "  promise from the `join` to take care of some computation that needs **all** of\n",
    "  a set of promises to be finished.\n",
    "\n",
    "* `Lwt.pick : 'a Lwt.t list -> 'a Lwt.t` also enables waiting upon multiple\n",
    "  promises, but `Lwt.pick ps` returns a promise that is pending until at least\n",
    "  one promise in `ps` becomes resolved. You might register a callback on the\n",
    "  return promise from the `pick` to take care of some computation that needs\n",
    "  just one of a set of promises to be finished, but doesn't care which one."
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "cell_metadata_filter": "-all",
   "formats": "md:myst",
   "text_representation": {
    "extension": ".md",
    "format_name": "myst",
    "format_version": 0.13,
    "jupytext_version": "1.10.3"
   }
  },
  "kernelspec": {
   "display_name": "OCaml",
   "language": "OCaml",
   "name": "ocaml-jupyter"
  },
  "source_map": [
   14
  ]
 },
 "nbformat": 4,
 "nbformat_minor": 5
}