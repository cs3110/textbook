{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "514a70cb",
   "metadata": {},
   "source": [
    "# Implementing Callbacks\n",
    "\n",
    "When a callback is registered with a promise using `bind` or one of the other syntaxes, it is\n",
    "added to a list of callbacks that is stored with the promise. Eventually, if\n",
    "the promise is fulfilled, the Lwt *resolution loop* runs all the callbacks\n",
    "registered with the promise. There is no guarantee about the execution order of\n",
    "callbacks for a promise. In other words, the execution order is\n",
    "nondeterministic. If the order matters, the programmer needs to use the\n",
    "composition operators (such as `bind` and `join`) to enforce an ordering. If the\n",
    "promise never becomes fulfilled (or is rejected), none of its callbacks will ever\n",
    "be run.\n",
    "\n",
    "```{note}\n",
    "Lwt also supports registering functions that are run after a promise is rejected.\n",
    "`Lwt.catch` and `try%lwt` are used for this purpose. They are counterparts\n",
    "to `Lwt.bind` and `let%lwt`.\n",
    "```\n",
    "\n",
    "Once again, it's important to keep track of where the concurrency really comes\n",
    "from: the OS. There might be many asynchronous I/O operations occurring at the\n",
    "OS level. But at the OCaml level, the resolution loop is sequential, meaning\n",
    "that only one callback can ever be running at a time.\n",
    "\n",
    "Finally, the resolution loop never attempts to interrupt a callback. So if one\n",
    "callback goes into an infinite loop, no other callback will ever get to run.\n",
    "That makes Lwt a cooperative concurrency mechanism, rather than a preemptive one.\n",
    "\n",
    "To better understand callback resolution, let's implement it ourselves. We'll\n",
    "use the `Promise` data structure we developed earlier. To start, we add a bind\n",
    "operator to the `Promise` signature:\n",
    "\n",
    "```ocaml\n",
    "module type PROMISE = sig\n",
    "  ...\n",
    "\n",
    "  (** [p >>= c] registers callback [c] with promise [p].\n",
    "      When the promise is fulfilled, the callback will be run\n",
    "      on the promises's contents.  If the promise is never\n",
    "      fulfilled, the callback will never run. *)\n",
    "  val ( >>= ) : 'a promise -> ('a -> 'b promise) -> 'b promise\n",
    "end\n",
    "```\n",
    "\n",
    "Next, let's re-develop the entire `Promise` structure.  We start\n",
    "off just like before:\n",
    "\n",
    "```ocaml\n",
    "module Promise : PROMISE = struct\n",
    "  type 'a state = Pending | Fulfilled of 'a | Rejected of exn\n",
    "  ...\n",
    "```\n",
    "\n",
    "But now to implement the representation type of promises, we use a record with\n",
    "mutable fields. The first field is the state of the promise, and it corresponds\n",
    "to the `ref` we used before. The second field is more interesting and is\n",
    "discussed below.\n",
    "\n",
    "```ocaml\n",
    "  (** RI: the input may not be [Pending]. *)\n",
    "  type 'a handler = 'a state -> unit\n",
    "\n",
    "  (** RI: if [state <> Pending] then [handlers = []]. *)\n",
    "  type 'a promise = {\n",
    "    mutable state : 'a state;\n",
    "    mutable handlers : 'a handler list\n",
    "  }\n",
    "```\n",
    "\n",
    "A *handler* is a new abstraction: a function that takes a state.\n",
    "The primary use for a handler will be to run callbacks.\n",
    "It will be used to fulfill and reject promises when their state is\n",
    "ready to switch away from pending. This is why we ask, via a representation\n",
    "invariant, that the input state to a handler may not be pending.\n",
    "\n",
    "We require that only pending promises may have handlers waiting in their list.\n",
    "Once the state becomes non-pending, i.e., either fulfilled or rejected,\n",
    "the handlers associated with the promise will all be processed and\n",
    "removed from the list.\n",
    "This is why we say, as a representation invariant, that if the state is not pending,\n",
    "then the handlers list must be empty.\n",
    "\n",
    "This helper function that enqueues a handler on a promise's handler list will be\n",
    "helpful later:\n",
    "\n",
    "```ocaml\n",
    "  let enqueue\n",
    "      (handler : 'a state -> unit)\n",
    "      (promise : 'a promise) : unit\n",
    "    =\n",
    "    promise.handlers <- handler :: promise.handlers\n",
    "```\n",
    "\n",
    "We continue to pun resolvers and promises internally:\n",
    "\n",
    "```ocaml\n",
    "  type 'a resolver = 'a promise\n",
    "```\n",
    "\n",
    "Because we changed the representation type from a `ref` to a record,\n",
    "we have to update a few of the functions in trivial ways:\n",
    "\n",
    "```ocaml\n",
    "  (** [write_once p s] changes the state of [p] to be [s].  If [p] and [s]\n",
    "      are both pending, that has no effect.\n",
    "      Raises: [Invalid_arg] if the state of [p] is not pending. *)\n",
    "  let write_once p s =\n",
    "    if p.state = Pending\n",
    "    then p.state <- s\n",
    "    else invalid_arg \"cannot write twice\"\n",
    "\n",
    "  let make () =\n",
    "    let p = {state = Pending; handlers = []} in\n",
    "    (p, p)\n",
    "\n",
    "  let return x =\n",
    "    {state = Fulfilled x; handlers = []}\n",
    "\n",
    "  let state p = p.state\n",
    "```\n",
    "\n",
    "Now we get to the trickier parts of the implementation.\n",
    "\n",
    "The steps needed to reject a promise (with an exception) or fulfill a promise\n",
    "(with a value) are quite similar, so we implement a helper function `resolve`.\n",
    "This helper takes a resolver and a state, and it changes the state of the associated promise to\n",
    "the given state.\n",
    "We require that the state `st` that we are moving over to may not be the pending state.\n",
    "We mutate the handlers list to be empty to ensure that the RI holds,\n",
    "but we save the handlers in a local variable.\n",
    "Then we call `write_once` on the resolver to change its state.\n",
    "Finally, we process all the handlers that were waiting on\n",
    "this promise. Each of those handlers requires a state for an input, and\n",
    "we pass them the new state that the promise has just been set to.\n",
    "\n",
    "```ocaml\n",
    "  (** Requires: [st] may not be [Pending]. *)\n",
    "  let resolve (r : 'a resolver) (st : 'a state) =\n",
    "    assert (st <> Pending);\n",
    "    let handlers = r.handlers in\n",
    "    r.handlers <- [];\n",
    "    write_once r st;\n",
    "    List.iter (fun f -> f st) handlers\n",
    "\n",
    "  let reject r e =\n",
    "    resolve r (Rejected e)\n",
    "\n",
    "  let fulfill r v =\n",
    "    resolve r (Fulfilled v)\n",
    "```\n",
    "\n",
    "Finally, the implementation of `>>=` is the trickiest part.\n",
    "Recall that the `bind` function needs to immediately return a new promise.\n",
    "If the input promise is already fulfilled, we need to *immediately*\n",
    "run the callback on it:\n",
    "we cannot enqueue a handler on the input promise, because that would violate the representation invariant by adding a handler to a non-pending promise.\n",
    "Running the callback immediately will either yield a new promise, which we immediately\n",
    "return; or the callback will raise an exception, in which case we create a new rejected promise\n",
    "containing the same exception and return that promise:\n",
    "\n",
    "```ocaml\n",
    "  let ( >>= )\n",
    "      (input_promise : 'a promise)\n",
    "      (callback : 'a -> 'b promise) : 'b promise\n",
    "    =\n",
    "    let fail exc = {state = Rejected exc; handlers = []} in\n",
    "    match input_promise.state with\n",
    "    | Fulfilled x -> (try callback x with exc -> fail exc)\n",
    "```\n",
    "\n",
    "Second, if the promise is already rejected with some exception, we craft a trivial new promise\n",
    "that is also rejected with the same exception.\n",
    "We return that new promise to the user immediately:\n",
    "```ocaml\n",
    "    | Rejected exc -> fail exc\n",
    "```\n",
    "\n",
    "Third, if the input promise is pending, we need to do more work.\n",
    "Our task is delicate: we need to immediately return a new promise\n",
    "(which we will call the output promise) to the user, but we also need that\n",
    "output promise to become fulfilled when (or if) the input promise becomes\n",
    "fulfilled and the callback completes running, sometime in the future.\n",
    "Its contents will be whatever contents are contained within the promise that the\n",
    "callback itself returns.\n",
    "\n",
    "So, we create a new promise and resolver\n",
    "called `output_promise` and `output_resolver`. That promise is what `bind`\n",
    "returns. Before returning it, we use a helper function `handler_of_callback`\n",
    "(described below) to transform the callback into a handler, and enqueue that\n",
    "handler on the promise. That ensures the handler will be run when the promise\n",
    "later becomes resolved:\n",
    "\n",
    "```ocaml\n",
    "    | Pending ->\n",
    "      let output_promise, output_resolver = make () in\n",
    "      enqueue (handler_of_callback callback output_resolver) input_promise;\n",
    "      output_promise\n",
    "```\n",
    "\n",
    "All that's left is to implement that helper function to create handlers out of\n",
    "callbacks. Recall that a handler's type is itself a *function type*,\n",
    "`'a state -> unit`. This is why our helper function's output is actally an\n",
    "anonymous function. That anonymous function takes a state as its input:\n",
    "\n",
    "```ocaml\n",
    "  let handler_of_callback\n",
    "      (callback : 'a -> 'b promise)\n",
    "      (resolver : 'b resolver) : 'a handler =\n",
    "      fun (state : 'a state) ->\n",
    "```\n",
    "\n",
    "We proceed by taking cases on that input state.\n",
    "The first two cases, below, are simple. It would violate the RI to\n",
    "call a handler on a pending state. And if the state is rejected, then the\n",
    "handler should propagate that rejection to the resolver, which causes the\n",
    "promise returned by bind to also be rejected.\n",
    "\n",
    "```ocaml\n",
    "  let handler_of_callback\n",
    "      (callback : 'a -> 'b promise)\n",
    "      (resolver : 'b resolver) : 'a handler =\n",
    "      fun (state : 'a state) ->\n",
    "      match state with\n",
    "      | Pending -> failwith \"handler RI violated\"\n",
    "      | Rejected exc -> reject resolver exc\n",
    "```\n",
    "\n",
    "But if the state is fulfiled, then the callback registered with the promise\n",
    "can&mdash;at last!&mdash;be run on the contents of the fulfilled promise. If the callback executes successfully it produces a new promise, but recall that the callback\n",
    "may itself raise an exception.\n",
    "\n",
    "First, consider the optimistic case in which the callback executes successfully and produces a promise. That promise might already be rejected or fulfilled,\n",
    "in which case that state again propagates.\n",
    "\n",
    "```ocaml\n",
    "      | Fulfilled x ->\n",
    "        let promise = callback x in\n",
    "        match promise.state with\n",
    "        | Fulfilled y -> resolve resolver y\n",
    "        | Rejected exc -> reject resolver exc\n",
    "```\n",
    "\n",
    "But the promise might still be pending.  In that case, we need to enqueue\n",
    "a new handler whose purpose is to do the propagation once the result is\n",
    "available:\n",
    "\n",
    "```ocaml\n",
    "        | Pending -> enqueue (copying_handler resolver) promise\n",
    "```\n",
    "\n",
    "where `copying_handler` is a new helper function that creates a very simple handler\n",
    "to do that propagation:\n",
    "\n",
    "```ocaml\n",
    "  let copying_handler (resolver : 'a resolver) : 'a handler\n",
    "    = function\n",
    "      | Pending -> failwith \"handler RI violated\"\n",
    "      | Rejected exc -> reject resolver exc\n",
    "      | Fulfilled x -> resolve resolver x\n",
    "```\n",
    "\n",
    "Second, consider the case in which the callback function itself raises some exception `exc`. In that case, we need to reject the promise with that exception. We do this by wrapping the execution of the callback in a `try` block:\n",
    "\n",
    "```ocaml\n",
    "      | Fulfilled x ->\n",
    "        try\n",
    "          let promise = callback x in\n",
    "          match promise.state with\n",
    "          | Fulfilled y -> resolve resolver y\n",
    "          | Rejected exc -> reject resolver exc\n",
    "          | Pending -> enqueue (copying_handler resolver) promise\n",
    "        with exc -> reject resolver exc\n",
    "```\n",
    "\n",
    "The Lwt implementation of `bind` follows essentially the same algorithm as we\n",
    "just implemented. Note that there is no concurrency in `bind`: as we said above,\n",
    "it's the OS that provides the concurrency.\n",
    "\n",
    "```{warning}\n",
    "Lwt makes a different choice than we did here when the input promise is already fulfilled and the callback raises an exception. In that case, Lwt has `bind` raise the exception itself, instead of returning a rejected promise that contains the exception. This is [known][lwt-bind-bug] to violate the specification of `bind` but is not going to be fixed for backward compatibility reasons.\n",
    "```\n",
    "\n",
    "[lwt-bind-bug]: https://discuss.ocaml.org/t/lwt-3-2-0-released-we-plan-to-change-lwt-bind/1337/13"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "cell_metadata_filter": "-all",
   "formats": "md:myst",
   "text_representation": {
    "extension": ".md",
    "format_name": "myst",
    "format_version": 0.13,
    "jupytext_version": "1.10.3"
   }
  },
  "kernelspec": {
   "display_name": "OCaml",
   "language": "OCaml",
   "name": "ocaml-jupyter"
  },
  "source_map": [
   14
  ]
 },
 "nbformat": 4,
 "nbformat_minor": 5
}