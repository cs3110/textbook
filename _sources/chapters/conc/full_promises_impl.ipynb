{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "fa548bce",
   "metadata": {},
   "source": [
    "# The Full Promises Implementation\n",
    "\n",
    "For convenience, here is the entire promises implementation in one executable block:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "d59a32eb",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "module type PROMISE =\n",
       "  sig\n",
       "    type 'a state = Pending | Fulfilled of 'a | Rejected of exn\n",
       "    type 'a promise\n",
       "    type 'a resolver\n",
       "    val make : unit -> 'a promise * 'a resolver\n",
       "    val return : 'a -> 'a promise\n",
       "    val state : 'a promise -> 'a state\n",
       "    val fulfill : 'a resolver -> 'a -> unit\n",
       "    val reject : 'a resolver -> exn -> unit\n",
       "    val ( >>= ) : 'a promise -> ('a -> 'b promise) -> 'b promise\n",
       "  end\n"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    },
    {
     "data": {
      "text/plain": [
       "module Promise : PROMISE\n"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "(** A signature for Lwt-style promises, with better names. *)\n",
    "module type PROMISE = sig\n",
    "  type 'a state =\n",
    "    | Pending\n",
    "    | Fulfilled of 'a\n",
    "    | Rejected of exn\n",
    "\n",
    "  type 'a promise\n",
    "\n",
    "  type 'a resolver\n",
    "\n",
    "  (** [make ()] is a new promise and resolver. The promise is pending. *)\n",
    "  val make : unit -> 'a promise * 'a resolver\n",
    "\n",
    "  (** [return x] is a new promise that is already fulfilled with value\n",
    "      [x]. *)\n",
    "  val return : 'a -> 'a promise\n",
    "\n",
    "  (** [state p] is the state of the promise. *)\n",
    "  val state : 'a promise -> 'a state\n",
    "\n",
    "  (** [fulfill r x] resolves the promise [p] associated with [r] with\n",
    "      value [x], meaning that [state p] will become [Fulfilled x].\n",
    "      Requires: [p] is pending. *)\n",
    "  val fulfill : 'a resolver -> 'a -> unit\n",
    "\n",
    "  (** [reject r x] rejects the promise [p] associated with [r] with\n",
    "      exception [x], meaning that [state p] will become [Rejected x].\n",
    "      Requires: [p] is pending. *)\n",
    "  val reject : 'a resolver -> exn -> unit\n",
    "\n",
    "  (** [p >>= c] registers callback [c] with promise [p].\n",
    "      When the promise is fulfilled, the callback will be run\n",
    "      on the promises's contents.  If the promise is never\n",
    "      fulfilled, the callback will never run. *)\n",
    "  val ( >>= ) : 'a promise -> ('a -> 'b promise) -> 'b promise\n",
    "end\n",
    "\n",
    "module Promise : PROMISE = struct\n",
    "  type 'a state = Pending | Fulfilled of 'a | Rejected of exn\n",
    "\n",
    "  (** RI: the input may not be [Pending]. *)\n",
    "  type 'a handler = 'a state -> unit\n",
    "\n",
    "  (** RI: if [state <> Pending] then [handlers = []]. *)\n",
    "  type 'a promise = {\n",
    "    mutable state : 'a state;\n",
    "    mutable handlers : 'a handler list\n",
    "  }\n",
    "\n",
    "  let enqueue\n",
    "      (handler : 'a state -> unit)\n",
    "      (promise : 'a promise) : unit\n",
    "    =\n",
    "    promise.handlers <- handler :: promise.handlers\n",
    "\n",
    "  type 'a resolver = 'a promise\n",
    "\n",
    "  (** [write_once p s] changes the state of [p] to be [s].  If [p] and [s]\n",
    "      are both pending, that has no effect.\n",
    "      Raises: [Invalid_arg] if the state of [p] is not pending. *)\n",
    "  let write_once p s =\n",
    "    if p.state = Pending\n",
    "    then p.state <- s\n",
    "    else invalid_arg \"cannot write twice\"\n",
    "\n",
    "  let make () =\n",
    "    let p = {state = Pending; handlers = []} in\n",
    "    p, p\n",
    "\n",
    "  let return x =\n",
    "    {state = Fulfilled x; handlers = []}\n",
    "\n",
    "  let state p = p.state\n",
    "\n",
    "  (** Requires: [st] may not be [Pending]. *)\n",
    "  let resolve (r : 'a resolver) (st : 'a state) =\n",
    "    assert (st <> Pending);\n",
    "    let handlers = r.handlers in\n",
    "    r.handlers <- [];\n",
    "    write_once r st;\n",
    "    List.iter (fun f -> f st) handlers\n",
    "\n",
    "  let reject r x =\n",
    "    resolve r (Rejected x)\n",
    "\n",
    "  let fulfill r x =\n",
    "    resolve r (Fulfilled x)\n",
    "\n",
    "  let copying_handler (resolver : 'a resolver) : 'a handler\n",
    "    = function\n",
    "      | Pending -> failwith \"handler RI violated\"\n",
    "      | Rejected exc -> reject resolver exc\n",
    "      | Fulfilled x -> fulfill resolver x\n",
    "\n",
    "  let handler_of_callback\n",
    "      (callback : 'a -> 'b promise)\n",
    "      (resolver : 'b resolver) : 'a handler =\n",
    "      fun (state : 'a state) ->\n",
    "      match state with\n",
    "        | Pending -> failwith \"handler RI violated\"\n",
    "        | Rejected exc -> reject resolver exc\n",
    "        | Fulfilled x ->\n",
    "          try\n",
    "            let promise = callback x in\n",
    "            match promise.state with\n",
    "            | Fulfilled y -> fulfill resolver y\n",
    "            | Rejected exc -> reject resolver exc\n",
    "            | Pending -> enqueue (copying_handler resolver) promise\n",
    "          with exc -> reject resolver exc\n",
    "\n",
    "  let ( >>= )\n",
    "      (input_promise : 'a promise)\n",
    "      (callback : 'a -> 'b promise) : 'b promise\n",
    "    =\n",
    "    let fail exc = {state = Rejected exc; handlers = []} in\n",
    "    match input_promise.state with\n",
    "    | Fulfilled x -> (try callback x with exc -> fail exc)\n",
    "    | Rejected exc -> fail exc\n",
    "    | Pending ->\n",
    "      let output_promise, output_resolver = make () in\n",
    "      enqueue (handler_of_callback callback output_resolver) input_promise;\n",
    "      output_promise\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "cell_metadata_filter": "-all",
   "formats": "md:myst",
   "text_representation": {
    "extension": ".md",
    "format_name": "myst",
    "format_version": 0.13,
    "jupytext_version": "1.10.3"
   }
  },
  "kernelspec": {
   "display_name": "OCaml",
   "language": "OCaml",
   "name": "ocaml-jupyter"
  },
  "language_info": {
   "codemirror_mode": "text/x-ocaml",
   "file_extension": ".ml",
   "mimetype": "text/x-ocaml",
   "name": "OCaml",
   "nbconverter_exporter": null,
   "pygments_lexer": "OCaml",
   "version": "5.3.0"
  },
  "source_map": [
   14,
   20
  ]
 },
 "nbformat": 4,
 "nbformat_minor": 5
}